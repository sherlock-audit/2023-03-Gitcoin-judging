ktg

high

# Vote function does not check if native token sent equal to amount in encodedVotes

## Summary

Vote function does not check if native token sent equal to amount in encodedVotes. This leads to 2 things: if users send `msg.value` more than amount in encodedVotes, their surplus native token are lost and grant address receive less than what they sent. If there is native token available in `QuadraticFundingVotingStrategyImplementation`  contract, malicious users could steal that funds by calling `vote` with `msg.value` < `amount` in `encodedVotes` 

## Vulnerability Detail
In the following example, we suppose that `RoundImplementation` uses `QuadraticFundingVotingStrategyImplementation`  as its voting strategy

If users call `vote` and send some native tokens, `RoundImplementation` forwards all that native tokens  to voting strategy:
```solidity
function vote(bytes[] memory encodedVotes) external payable {
    // slither-disable-next-line timestamp
    require(
      roundStartTime <= block.timestamp &&
      block.timestamp <= roundEndTime,
      "Round: Round is not active"
    );

    votingStrategy.vote{value: msg.value}(encodedVotes, msg.sender);
  }
```

However, function `vote` of `QuadraticFundingVotingStrategyImplementation` does not check if amount in `encodedVotes` matches received token value:
```solidity
function vote(bytes[] calldata encodedVotes, address voterAddress) external override payable nonReentrant isRoundContract {

    /// @dev iterate over multiple donations and transfer funds
    for (uint256 i = 0; i < encodedVotes.length; i++) {

      /// @dev decode encoded vote
      (
        address _token,
        uint256 _amount,
        address _grantAddress,
        bytes32 _projectId
      ) = abi.decode(encodedVotes[i], (
        address,
        uint256,
        address,
        bytes32
      ));

      if (_token == address(0)) {
        /// @dev native token transfer to grant address
        // slither-disable-next-line reentrancy-events
        AddressUpgradeable.sendValue(payable(_grantAddress), _amount);
      } else {

        /// @dev erc20 transfer to grant address
        // slither-disable-next-line arbitrary-send-erc20,reentrancy-events,
        SafeERC20Upgradeable.safeTransferFrom(
          IERC20Upgradeable(_token),
          voterAddress,
          _grantAddress,
          _amount
        );

      }

      /// @dev emit event for transfer
      emit Voted(
        _token,
        _amount,
        voterAddress,
        _grantAddress,
        _projectId,
        msg.sender
      );

    }

  }
```
Contract `QuadraticFundingVotingStrategyImplementation ` also has no function to allow transfer back stuck native tokens
## Impact
- Voters can loose native tokens if they accidentally send more than configured in `encodedVotes`, the grantAddress receives less than what voters send
- If voters send more native tokens than required, others can steal these tokens by calling `vote` with `grantAddress` they control
- Malicious users can use this to steal native tokens stuck in voting strategy contract 

## Code Snippet
https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/round/RoundImplementation.sol#L430-#L439

https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/votingStrategy/QuadraticFundingStrategy/QuadraticFundingVotingStrategyImplementation.sol#L55-#L102

## Tool used

Manual Review

## Recommendation
I recommend checking if total `amount` in `encodedVotes` matches the amount voting strategy received. If there's surplus, transfer back to the voter, if `msg.value` is less than total `amount`, revert.