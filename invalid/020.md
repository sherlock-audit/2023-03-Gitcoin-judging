sashik_eth

medium

# Call to setReadyForPayout() can be front-runned with protocol fee increase

## Summary

The administrator of the protocol may unintentionally front-run the `setReadyForPayout()` transaction of the round operator by executing a transaction that increases the protocol fee.

## Vulnerability Detail

When the round ends operator calls `setReadyForPayout()` function which pays the protocol fee and transfers the matching pool to the Payout Contract, where it is held until funds are distributed. Since all rounds due to protocol architecture call the same instance of `AlloSettings.sol` when calculating the protocol fee, the next scenario is possible:

1. Operator call `setReadyForPayout()`. Based on the current value of `protocolFeePercentage` 1% and `roundFeePercentage` 0%, the expected value received by the payout contract is 99% of the collected funds.
2. Protocol admin call `updateProtocolFeePercentage()`, setting the value of `protocolFeePercentage` to 2%, accidentally front-running the operator's transaction.
3. Payout contract receives 98% of the funds instead of the expected 99%.

## Impact

Round recipients would receive less funds than the round operator expected.

## Code Snippet

Owner of AlloSettings.sol could instantly update `protocolFeePercentage`:
https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/settings/AlloSettings.sol#L40
```solidity
  function updateProtocolFeePercentage(uint24 _protocolFeePercentage) external onlyOwner { 

    require(_protocolFeePercentage <= DENOMINATOR , "value exceeds 100%");

    protocolFeePercentage = _protocolFeePercentage;
    emit ProtocolFeePercentageUpdated(protocolFeePercentage);
  }
```

`setReadyForPayout` function uses the currently stored value of `protocolFeePercentage` in fee calculation:
https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/round/RoundImplementation.sol#L443
```solidity
  function setReadyForPayout() external payable roundHasEnded onlyRole(ROUND_OPERATOR_ROLE) {
    uint256 fundsInContract = _getTokenBalance(token);
    uint32 denominator = alloSettings.DENOMINATOR();

    uint256 protocolFeeAmount = (matchAmount * alloSettings.protocolFeePercentage()) / denominator;
    uint256 roundFeeAmount = (matchAmount * roundFeePercentage) / denominator;

    // total funds needed for payout
    uint256 neededFunds = matchAmount + protocolFeeAmount + roundFeeAmount;

    require(fundsInContract >= neededFunds, "Round: Not enough funds in contract");

    // deduct protocol fee
    if (protocolFeeAmount > 0) {
      address payable protocolTreasury = alloSettings.protocolTreasury();
      _transferAmount(protocolTreasury, protocolFeeAmount, token);
    }

    // deduct round fee
    if (roundFeeAmount > 0) {
      _transferAmount(roundFeeAddress, roundFeeAmount, token);
    }

    // update funds in contract after fee deduction
    fundsInContract = _getTokenBalance(token);

    // transfer funds to payout contract
    if (token == address(0)) {
      payoutStrategy.setReadyForPayout{value: fundsInContract}();
    } else {
      IERC20(token).safeTransfer(address(payoutStrategy), fundsInContract);
      payoutStrategy.setReadyForPayout();
    }

    emit PayFeeAndEscrowFundsToPayoutContract(fundsInContract, protocolFeeAmount, roundFeeAmount);
  }
```

## Tool used

Manual Review

## Recommendation

Consider introducing an upper limit for fees so users can know the maximum fees available in protocol and adding a timelock to change fee size. Alternatively, the current fee size could be saved as a static value in each round's storage during deployment.
This way, front-running will be impossible, and users will know which fee they agree to. 


