sashik_eth

medium

# Chain reorg enables creation of a round with the same address.

## Summary

If a reorg incident occurs on the chain where the Allo protocol is deployed, an attacker can redeploy a round contract with malicious logic on the same address that the user expects to be their own.

## Vulnerability Detail

New round contracts deploy in ```RoundFactory.sol```  using function ```clone```  from the OZ ClonesUpgradeable library. Within the `clone` function, a `create` call generates a new contract with an address that depends only on the creator's nonce (in this case, RoundFactory's). 
Since this process is permissionless, any user can create a new round. If the RoundFactory's nonce is the same, a round with the same address will be created regardless of the factory's called parameters. 
This opens attack vector on chains that have possibility of reorg happens and Allo protocol already [deployed](https://docs.allo.gitcoin.co/getting-started/contracts), Optimism, for example.

Consider the next scenario:
1. User creates a new round contract for grant distribution by calling ```creates()``` on RoundFactory and receives the address of the newly created round as a successful result.
2. However, a reorg occurs on the chain causing the RoundFactory nonce counter to roll back.
3. An attacker who is monitoring the chain notices that the reorg has occurred and calls `creates()` on RoundFactory with modified parameters, including a malicious `_initAddress.votingStrategy` and/or `_initAddress.payoutStrategy` that collects all sent funds for the attacker. The factory creates a new round for the attacker with the same address as the user's.
4. The user is not informed about the reorg and mistakenly treats the attacker's round address as their own, leading to the theft of all donated funds and/or funds that would be sent to the round address as `matchAmount`.

## Impact

Round contract address that created by user could be recreated by attacker in case of reorg incedent on the chain. This could lead to steailng all round collected funds.
Additionally, deployments of  new program also vulnurable for this type attack, but more considerable potential damage expected in case of attack on deployed rounds.

## Code Snippet

Function that creates new rounds:
https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/round/RoundFactory.sol#L92

```solidity
  function create(
    bytes calldata encodedParameters,
    address ownedBy
  ) external returns (address) {

    require(roundImplementation != address(0), "roundImplementation is 0x");
    require(alloSettings != address(0), "alloSettings is 0x");

    address clone = ClonesUpgradeable.clone(roundImplementation); 

    emit RoundCreated(clone, ownedBy, payable(roundImplementation));

    IRoundImplementation(payable(clone)).initialize(
      encodedParameters,
      alloSettings
    );

    return clone;
  }
```

OZ cloning function use create:
https://github.com/OpenZeppelin/openzeppelin-contracts-upgradeable/blob/master/contracts/proxy/ClonesUpgradeable.sol#L25
```solidity
    function clone(address implementation) internal returns (address instance) {
        /// @solidity memory-safe-assembly
        assembly {
            // Cleans the upper 96 bits of the `implementation` word, then packs the first 3 bytes
            // of the `implementation` address with the bytecode before the address.
            mstore(0x00, or(shr(0xe8, shl(0x60, implementation)), 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000))
            // Packs the remaining 17 bytes of `implementation` with the bytecode after the address.
            mstore(0x20, or(shl(0x78, implementation), 0x5af43d82803e903d91602b57fd5bf3))
            instance := create(0, 0x09, 0x37)
        }
        require(instance != address(0), "ERC1167: create failed");
    }

```

Program factory also could be exploited since using `clone()`:
https://github.com/sherlock-audit/2023-03-Gitcoin/blob/main/contracts/contracts/program/ProgramFactory.sol#L49
```solidity
  function create(
    bytes calldata encodedParameters
  ) external returns (address) {

    address clone = ClonesUpgradeable.clone(programContract);
    emit ProgramCreated(clone, programContract);
    ProgramImplementation(clone).initialize(encodedParameters);

    return clone;
  }
```

## Tool used

Manual Review

## Recommendation

Consider using ```cloneDeterministic()``` function from OZ ClonesUpgradeable library (which uses ```create2``` call) with adding ```msg.sender``` as a salt parameter for deploying new rounds instead of using ```clone()```.
This would ensure that different users receive different `round` addresses, even if the RoundFactory contract's nonce is the same.