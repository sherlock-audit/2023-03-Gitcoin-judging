p0wd3r

high

# The initialization of strategy can be front-runned.

## Summary
The initialization of strategy can be front-runned, leading to DoS and even user asset loss in specific situations.
## Vulnerability Detail
The `init` function in IVotingStrategy.sol and IPayoutStrategy.sol are both external functions without any permission restrictions, therefore an attacker can front-run before the round initialization.
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/payoutStrategy/IPayoutStrategy.sol#L92
```solidity
function init() external {
    require(roundAddress == address(0x0), "roundAddress already set");
    roundAddress = payable(msg.sender);

    // set the token address
    tokenAddress = RoundImplementation(roundAddress).token();

    isReadyForPayout = false;
}
```
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/votingStrategy/IVotingStrategy.sol#L36
```solidity
function init() external {
    require(roundAddress == address(0), "init: roundAddress already set");
    roundAddress = msg.sender;
}
```

If the attacker performs a front-run, it will cause a revert during round initialization, resulting in a DoS of the service.
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/round/RoundImplementation.sol#L255
```solidity
function initialize(
    bytes calldata encodedParameters,
    address _alloSettings
  ) external initializer {
...
votingStrategy.init();
payoutStrategy.init();
...
}
```

Additionally, if the user grants authorization to the voteStrategy contract before initializing the round, then an attacker can directly transfer the user's assets.
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/votingStrategy/QuadraticFundingStrategy/QuadraticFundingVotingStrategyImplementation.sol#L55
```solidity
function vote(bytes[] calldata encodedVotes, address voterAddress) external override payable nonReentrant isRoundContract {
...
/// @dev erc20 transfer to grant address
        // slither-disable-next-line arbitrary-send-erc20,reentrancy-events,
        SafeERC20Upgradeable.safeTransferFrom(
          IERC20Upgradeable(_token),
          voterAddress,
          _grantAddress,
          _amount
        );
...
}
```
Specifically, after front-running the `init` function, the attacker immediately constructs parameters to execute the `vote` function. If the user has approved this contract, then their assets will be taken away.

## Impact
DoS and even user asset loss in specific situations
## Code Snippet
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/payoutStrategy/IPayoutStrategy.sol#L92
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/votingStrategy/IVotingStrategy.sol#L36
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/round/RoundImplementation.sol#L255
https://github.com/allo-protocol/contracts/blob/36dc33762c396660c0a84f6ef7d790f632638e81/contracts/votingStrategy/QuadraticFundingStrategy/QuadraticFundingVotingStrategyImplementation.sol#L55
## Tool used

Manual Review

## Recommendation
Since the strategy is bound to the round, it can be created and bound to the round after initialization.